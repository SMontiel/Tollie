apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'java-library'
group = 'com.github.smontiel'

def Properties props = new Properties()
props.load(new FileInputStream(file('../local.properties')))

ext.android = [
        sdk   : props["sdk.dir"],
        target: 'android-26'
]

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compile files("${android.sdk}/platforms/${android.target}/android.jar")

    compile 'io.reactivex:rxjava:1.3.3'
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}